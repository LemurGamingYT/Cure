enum Location {
    MEADOW, CAVE, FOREST
}

array[string] inventory = string{}
Location location = Location.MEADOW


func get_action(array[string] options) -> string {
    if !options.has("quit") {
        options.add("quit")
    }

    return input($"What do you do? ({'/'.join(options)}): ")
}

func handle_meadow() -> bool {
    available_items = {"torch", "rope"}
    if inventory.has("torch") {
        available_items.remove("torch")
    }

    if inventory.has("rope") {
        available_items.remove("rope")
    }

    items = $" You see {' and '.join(available_items)}." if available_items.length > 0 else ""
    print($"You're at the entrance of a dark cave in a meadow.{items}")
    action = get_action(string{"take torch", "take rope", "enter cave", "walk away"})
    if action == "take torch" {
        inventory.add("torch")
        print("You picked up the torch.")
    } else if action == "take rope" {
        inventory.add("rope")
        print("You picked up the rope.")
    } else if action == "enter cave" {
        location = Location.CAVE
    } else if action == "walk away" {
        location = Location.FOREST
    } else if action == "quit" {
        return false
    } else {
        print("Invalid action")
    }

    return true
}

func handle_cave() -> bool {
    print("You're in a dark cave.")
    if inventory.has("torch") {
        print("Your torch illuminates the cave and you see that there is a deep chasm ahead.")
    } else {
        print("It's really dark. You could really use a light.")
        location = Location.MEADOW
        return true
    }

    action = get_action(string{"cross chasm", "go back"})
    if action == "cross chasm" {
        if inventory.has("rope") {
            print("You use the rope to cross the chasm and find a treasure chest! You win!")
            return false
        } else {
            print("You fall into chasm. Game over!")
            return false
        }
    } else if action == "go back" {
        location = Location.MEADOW
    } else if action == "quit" {
        return false
    } else {
        print("Invalid action.")
    }

    return true
}

func handle_forest() -> bool {
    print("You enter a damp forest. You walk past a tree carved with 'Do not-', being cut off.")
    action = get_action(string{"inspect the trees", "go back to the meadow"})
    if action == "go back to the meadow" {
        location = Location.MEADOW
    } else if action == "inspect the trees" {
        beware = $"'Beware of {System.TERMINAL_BOLD}him{System.TERMINAL_RESET}'"
        print($"You inspect the trees and find another says {beware}. You then continue walking.")
    } else if action == "quit" {
        return false
    } else {
        print("Invalid action.")
    }

    return true
}


func main() -> int {
    keep_running = true
    while keep_running {
        if location == Location.MEADOW {
            keep_running = handle_meadow()
        } else if location == Location.CAVE {
            keep_running = handle_cave()
        } else if location == Location.FOREST {
            keep_running = handle_forest()
        }
    }

    print("Thanks for playing!")
    return 0
}
